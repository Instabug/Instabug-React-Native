apply plugin: 'com.android.library'

apply from: './jacoco.gradle'
apply from: './native.gradle'
apply from: './sourcemaps.gradle'

/* (Preparing to support RN 0.73) Checking APG version to be backward-compatible with RN versions < 0.71 */
def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
def shouldUseNameSpace = agpVersion >= 7
def PACKAGE_PROP = "package=\"com.instabug.reactlibrary\""
def manifestOutFile = file("${projectDir}/src/main/AndroidManifest.xml")
def manifestContent = manifestOutFile.getText()
def isManifestContentUpdated = manifestOutFile.getText() != manifestContent
def isPackageNamespaceMissing = !manifestContent.contains("$PACKAGE_PROP")

String getExtOrDefault(String name) {
    def defaultPropertyKey = 'InstabugReactNative_' + name
    if (rootProject.ext.has(name)) {
        return rootProject.ext.get(name)
    }
    return project.properties[defaultPropertyKey]
}

if(shouldUseNameSpace){
    manifestContent = manifestContent.replaceAll(
        PACKAGE_PROP,
        ''
    )  
} else if(isPackageNamespaceMissing) {
    manifestContent = manifestContent.replace(
        '<manifest',
        "<manifest $PACKAGE_PROP "
    )
}

manifestContent.replaceAll("  ", " ")

if(isManifestContentUpdated){
    manifestOutFile.write(manifestContent)
}

android {
    if (shouldUseNameSpace){
        namespace = "com.instabug.reactlibrary"
    }
    compileSdkVersion getExtOrDefault('compileSdkVersion').toInteger()
    buildToolsVersion getExtOrDefault('buildToolsVersion')

    defaultConfig {
        minSdkVersion getExtOrDefault('minSdkVersion').toInteger()
        targetSdkVersion getExtOrDefault('targetSdkVersion').toInteger()
        versionCode 1
        versionName "11.13.0"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        consumerProguardFiles 'proguard-rules.txt'
    }
    lintOptions {
       warning 'InvalidPackage'
        abortOnError true
        // SuppressLint WrongConstant was used to suppress errors when using arrays of ints to represent annotations.
    }
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'com.facebook.react:react-native:+'

    testImplementation "org.mockito:mockito-inline:3.12.1"
    testImplementation "org.mockito:mockito-android:3.4.0"
    testImplementation 'junit:junit:4.13.2'
}
